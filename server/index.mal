; it's PHP jim, but not as we know it.
(def! CSV-HEADER "timestamp,comment\n")
(def! WRITE-FLAGS 10) ;FILE_APPEND | LOCK_EX
(def! DATA-DIR "../data/")

(def! response
  (fn* [content-type content]
       (do
         (! header (str "Content-type: " content-type))
         (println content))))

(def! throw-error
  (fn* [c e]
       (do (! header "Content-type: application/json") 
           (! http_response_code c)
           (println (! json_encode {"error" e}))
           (php* "exit(1);"))))

(def! console-dump
  (fn* [v]
       (! error_log (! print_r v true))))

(def! get-valid
  (fn* [m k]
       (let* [v (get m k)]
         (if v (if (not (= v "")) v)))))

(def! ensure-csv-exists
  (fn* [filename]
       (if (not (! is_file filename))
         (! file_put_contents filename CSV-HEADER WRITE-FLAGS))))

(def! append-event-to-file
  (fn* [filename event-type timestamp event-comment]
       (let* [contents (str timestamp "," (if event-comment (! json_encode event-comment) "") "\n")]
         (do
           ; ensure the file has a CSV header
           (ensure-csv-exists filename)
           ; stamp this event into the file
           (! file_put_contents filename contents WRITE-FLAGS)
           ; tell the user what we did
           (! json_encode {:success true :event-type event-type :timestamp timestamp :comment event-comment})))))

(def! create-event-csv
  (fn* [event-type filename]
       (! json_encode {:success true :event-type event-type :created (ensure-csv-exists filename)})))

(def! log-new-event
  (fn* [data]
       (let* [event-type (get-valid data "event")
              event-type-sanitised (if event-type (! str_replace ["." "/" "\\" ":"] "" event-type))
              filename (if event-type-sanitised (str DATA-DIR event-type-sanitised ".csv"))
              event-comment (get-valid data "comment")
              timestamp (get-valid data "timestamp")
              create (get-valid data "create")]
         (cond (nil? event-type-sanitised) (throw-error 404 "No event supplied.")
               create (create-event-csv event-type-sanitised filename)
               (nil? timestamp) (throw-error 404 "No timestamp supplied.")
               true (append-event-to-file filename event-type-sanitised timestamp event-comment)))))

(def! concatenate-all-csvs
  (fn* [events]
       ; add the CSV header with the extra "event" field for the filename
       (str "event," CSV-HEADER
            ; join together each parsed csv file
            (! implode "\n"
               (map
                 ; for every csv/event filename
                 (fn* [event]
                      ; grab the content of the csv
                      (let* [csv-content (! file_get_contents (str DATA-DIR event ".csv"))]
                        ; join the lines of the csv back together again
                        (! implode "\n"
                           ; loop through every line and add the event name
                           (map (fn* [line] (str event "," line))
                                ; filter out empty lines
                                (vals (! array_filter
                                         ; chop off the headline of the CSV as we already added one
                                         (rest (! explode "\n" csv-content))
                                         ; (function to filter out empty lines)
                                         (fn* [line] (not (or (= line "") (= line nil))))))))))
                 events)))))

(def! filter-for-csvs
  (fn* [files]
       (vals (! array_filter
                files
                (fn* [f]
                     (= (! substr f -4 4) ".csv"))))))

(def! process-http-request
  (fn* [vars]
       (let* [files (! scandir DATA-DIR)
              csvs (filter-for-csvs files)
              event-names (map (fn* [f] (! substr f 0 -4)) csvs)]
         (cond
           ; a get request always returns a list of available CSVs
           (= (get vars "REQUEST_METHOD") "GET")
           (if (not (nil? (get ($ "_GET") "all")))
             (response "text/csv" (concatenate-all-csvs event-names))
             (response "application/json" (! json_encode event-names)))

           ; a PUT request writes a new line to the end of a CSV
           (= (get vars "REQUEST_METHOD") "POST")
           (response "application/json" (log-new-event ($ "_POST")))))))

; *** main entry point *** ;

(if (! is_dir DATA-DIR)
  (process-http-request ($ "_SERVER"))
  (throw-error 404 "Data directory does not exist."))

